//@version=5
indicator(title="Relative Volatility Index", shorttitle="RVI", format=format.price, precision=2, timeframe="", timeframe_gaps=true)
length = input.int(10, minval=1)
offset = input.int(0, "Offset", minval = -500, maxval = 500)
maTypeInput = input.string("SMA", title="MA Type", options=["SMA", "Bollinger Bands", "EMA", "SMMA (RMA)", "WMA", "VWMA"], group="MA Settings")
maLengthInput = input.int(14, title="MA Length", group="MA Settings")
bbMultInput = input.float(2.0, minval=0.001, maxval=50, title="BB StdDev", group="MA Settings")
src = close
len = 14
stddev = ta.stdev(src, length)
upper = ta.ema(ta.change(src) <= 0 ? 0 : stddev, len)
lower = ta.ema(ta.change(src) > 0 ? 0 : stddev, len)
rvi = upper / (upper + lower) * 100
ma(source, length, type) =>
    switch type
        "SMA" => [ta.sma(source, length), na, na]
        "Bollinger Bands" =>
            [middleValue, highValue, lowValue] = ta.bb(source, length, bbMultInput)
            [middleValue, highValue, lowValue]
        "EMA" => [ta.ema(source, length), na, na]
        "SMMA (RMA)" => [ta.rma(source, length), na, na]
        "WMA" => [ta.wma(source, length), na, na]
        "VWMA" => [ta.vwma(source, length), na, na]

[rviMA,highValue,lowValue] = ma(rvi, maLengthInput, maTypeInput)

h0 = hline(80, "Upper Band", color=#787B86)
hline(50, "Middle Band", color=color.new(#787B86, 50))
h1 = hline(20, "Lower Band", color=#787B86)
fill(h0, h1, color=color.rgb(126, 87, 194, 90), title="Background")

plot(rvi, title="RVI", color=#7E57C2, offset = offset)
plot(rviMA, "RVI-based MA", color=color.yellow, offset = offset)

bbUpper = plot(highValue, title="Upper Bollinger Band", color=color.green)
bbLower = plot(lowValue, title="Lower Bollinger Band", color=color.green)
fill(bbUpper, bbLower, color = color.new(color.green, 90), title="Bollinger Bands Background Fill")